plugins {
    id 'java'
    id 'war'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.tini'
version = '0.0.1-SNAPSHOT'
description = 'tiniProjectBackend'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}
ext {
    querydslVersion = "5.0.0"
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat-runtime'

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // DB (MongoDB, mariaDB)
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

    // jpa
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "com.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
    annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
    annotationProcessor "jakarta.annotation:jakarta.annotation-api"
    annotationProcessor "jakarta.persistence:jakarta.persistence-api"


    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

//gradle build 시 QClass (Q엔티티클래스명) 소스도 함께 build 하기 위해서 sourceSets 에 저장할 위치를 추가 지정해 준다.
def querydslSrcDir = 'src/main/generated'

sourceSets {
    main {
        java {
            srcDirs += [ querydslSrcDir ]
        }
    }
}

tasks.withType(JavaCompile) {
    options.generatedSourceOutputDirectory = file(querydslSrcDir)
}

clean {
    delete file(querydslSrcDir)
}

tasks.named('test') {
    useJUnitPlatform()
}
